#version 460
#extension GL_EXT_nonuniform_qualifier : enable

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform samplerCube skyboxes[];
layout(set = 0, binding = 5, rgba32f) uniform writeonly imageCube outputPrefilter[];

layout(push_constant) uniform PushConstants {
    uint inputIdx;
    uint outputIdx;
    float roughness;
} pc;

const float PI = 3.14159265359;

float RadicalInverse_Vdc(uint bits) {
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}

vec2 Hammersley(uint i, uint N) {
    return vec2(float(i)/float(N), RadicalInverse_Vdc(i));
}

vec3 ImportanceSampleGGX(vec2 Xi, vec3 N, float roughness) {
    float a = roughness*roughness;
    
    float phi = 2.0 * PI * Xi.x;
    float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a*a - 1.0) * Xi.y));
    float sinTheta = sqrt(1.0 - cosTheta*cosTheta);
    
    // from spherical coordinates to cartesian coordinates
    vec3 H;
    H.x = cos(phi) * sinTheta;
    H.y = sin(phi) * sinTheta;
    H.z = cosTheta;
    
    // from tangent-space vector to world-space sample vector
    vec3 up          = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent   = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);
    
    vec3 sampleVec = tangent * H.x + bitangent * H.y + N * H.z;
    return normalize(sampleVec);
}

vec3 getCubeDir(vec2 uv, uint face) {
    vec3 dir;
    if (face == 0) dir = vec3(1.0,  uv.y, -uv.x); // +X
    if (face == 1) dir = vec3(-1.0, uv.y,  uv.x); // -X
    if (face == 2) dir = vec3(uv.x, -1.0,  uv.y); // +Y
    if (face == 3) dir = vec3(uv.x,  1.0, -uv.y); // -Y
    if (face == 4) dir = vec3(uv.x,  uv.y,  1.0); // +Z
    if (face == 5) dir = vec3(-uv.x, uv.y, -1.0); // -Z
    return normalize(dir);
}

void main() {
    ivec2 outputSize = imageSize(outputPrefilter[nonuniformEXT(pc.outputIdx)]).xy;
    if (gl_GlobalInvocationID.x >= outputSize.x || gl_GlobalInvocationID.y >= outputSize.y) return;

    vec2 uv = (vec2(gl_GlobalInvocationID.xy) + 0.5) / vec2(outputSize) * 2.0 - 1.0;
    vec3 N = getCubeDir(uv, gl_GlobalInvocationID.z);
    vec3 R = N;
    vec3 V = R;

    const uint SAMPLE_COUNT = 1024u;
    float totalWeight = 0.0;
    vec3 prefilteredColor = vec3(0.0);
    
    for(uint i = 0u; i < SAMPLE_COUNT; ++i) {
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        vec3 H  = ImportanceSampleGGX(Xi, N, pc.roughness);
        vec3 L  = normalize(2.0 * dot(V, H) * H - V);

        float NdotL = max(dot(N, L), 0.0);
        if(NdotL > 0.0) {
            prefilteredColor += texture(skyboxes[nonuniformEXT(pc.inputIdx)], L).rgb * NdotL;
            totalWeight      += NdotL;
        }
    }
    prefilteredColor = prefilteredColor / totalWeight;

    imageStore(outputPrefilter[nonuniformEXT(pc.outputIdx)], ivec3(gl_GlobalInvocationID.xyz), vec4(prefilteredColor, 1.0));
}
