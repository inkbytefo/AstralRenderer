#version 460
#extension GL_EXT_nonuniform_qualifier : enable

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform samplerCube skyboxes[];
layout(set = 0, binding = 5, rgba32f) uniform writeonly imageCube outputIrradiance[];

layout(push_constant) uniform PushConstants {
    uint inputIdx;
    uint outputIdx;
} pc;

const float PI = 3.14159265359;

vec3 getCubeDir(vec2 uv, uint face) {
    vec3 dir;
    if (face == 0) dir = vec3(1.0,  uv.y, -uv.x); // +X
    if (face == 1) dir = vec3(-1.0, uv.y,  uv.x); // -X
    if (face == 2) dir = vec3(uv.x, -1.0,  uv.y); // +Y
    if (face == 3) dir = vec3(uv.x,  1.0, -uv.y); // -Y
    if (face == 4) dir = vec3(uv.x,  uv.y,  1.0); // +Z
    if (face == 5) dir = vec3(-uv.x, uv.y, -1.0); // -Z
    return normalize(dir);
}

void main() {
    ivec2 outputSize = imageSize(outputIrradiance[nonuniformEXT(pc.outputIdx)]).xy;
    if (gl_GlobalInvocationID.x >= outputSize.x || gl_GlobalInvocationID.y >= outputSize.y) return;

    vec2 uv = (vec2(gl_GlobalInvocationID.xy) + 0.5) / vec2(outputSize) * 2.0 - 1.0;
    vec3 normal = getCubeDir(uv, gl_GlobalInvocationID.z);

    vec3 irradiance = vec3(0.0);

    vec3 up    = vec3(0.0, 1.0, 0.0);
    vec3 right = normalize(cross(up, normal));
    up         = normalize(cross(normal, right));

    float sampleDelta = 0.025;
    float nrSamples = 0.0;
    for(float phi = 0.0; phi < 2.0 * PI; phi += sampleDelta) {
        for(float theta = 0.0; theta < 0.5 * PI; theta += sampleDelta) {
            // spherical to cartesian (in tangent space)
            vec3 tangentSample = vec3(sin(theta) * cos(phi),  sin(theta) * sin(phi), cos(theta));
            // tangent space to world
            vec3 sampleVec = tangentSample.x * right + tangentSample.y * up + tangentSample.z * normal;

            irradiance += texture(skyboxes[nonuniformEXT(pc.inputIdx)], sampleVec).rgb * cos(theta) * sin(theta);
            nrSamples++;
        }
    }
    irradiance = PI * irradiance * (1.0 / float(nrSamples));

    imageStore(outputIrradiance[nonuniformEXT(pc.outputIdx)], ivec3(gl_GlobalInvocationID.xyz), vec4(irradiance, 1.0));
}
